module conditions {

  var ops_i, ops_j: common.instr_pair_t;
  var latencies_i, latencies_j: common.dual_latencies_t;
  var stages_i: common.dual_stage_t;  

  instance cond : test(cond_ops_i: (ops_i), cond_ops_j: (ops_j),
  	   	       cond_latencies_i: (latencies_i),
		       cond_latencies_j: (latencies_j),
		       cond_stages_i: (stages_i));

  init {
    // Assume all types of opcodes
    assume(ops_i.I_instr == nop || ops_i.I_instr == mac_op || ops_i.I_instr == other_op);
    assume(ops_i.LS_instr == nop || ops_i.LS_instr == load_store_op || ops_i.LS_instr == other_op);
    assume(ops_j.I_instr == nop || ops_j.I_instr == mac_op || ops_j.I_instr == other_op);
    assume(ops_j.LS_instr == nop || ops_j.LS_instr == load_store_op || ops_j.LS_instr == other_op);
    
    // Assume latencies for instructions in IF stage
    // Both pipelines reach IF at the same time by design
	// Same latency assuming same cache line for I and LS instr.: this is a simplification as a given first instruction could be on a cache line while the other on the next one (i.e. cache miss potentially)
    assume(latencies_i.LS_pipe.IF == latencies_i.I_pipe.IF);
    assume(latencies_j.LS_pipe.IF == latencies_j.I_pipe.IF);
    
    // For previous instructions (i)
    // If IF stage not reached yet, then this will be either a cache hit (possibly stalled for any hazard) or a cache miss
    assume(stages_i.I_pipe == pre ==> ((0 < latencies_i.I_pipe.IF && latencies_i.I_pipe.IF <= param.max_stalling) || latencies_i.I_pipe.IF == param.max_latency));
    // If initially within IF stage, full range
    assume(stages_i.I_pipe == IF ==> (0 < latencies_i.I_pipe.IF && (latencies_i.I_pipe.IF <= param.max_latency || latencies_i.I_pipe.IF <= param.max_stalling)));
    
    // Current instructions (j) start both at pre and will be either a cache hit or a cache miss in IF
    assume((0 < latencies_j.I_pipe.IF && latencies_j.I_pipe.IF <= param.max_stalling) || latencies_j.I_pipe.IF == param.max_latency);

    // Assume latencies for ID stage
    // Possibly stalled for any hazard
    assume(0 < latencies_i.I_pipe.ID && latencies_i.I_pipe.ID <= param.max_stalling);
    // LS pipeline always stalled for at least as much as I
    assume(latencies_i.I_pipe.ID <= latencies_i.LS_pipe.ID && latencies_i.LS_pipe.ID <= param.max_stalling);
    assume(0 < latencies_j.I_pipe.ID && latencies_j.I_pipe.ID <= param.max_stalling);
    assume(latencies_j.I_pipe.ID <= latencies_j.LS_pipe.ID && latencies_j.LS_pipe.ID <= param.max_stalling);
    
    // Assume latencies for EX stage apart from load/store ops
    assume(0 < latencies_i.I_pipe.EX && latencies_i.I_pipe.EX <= param.max_stalling);
    // Specificity for load_store (cache hit/miss)
    assume(ops_i.LS_instr != load_store_op ==> (latencies_i.I_pipe.EX <= latencies_i.LS_pipe.EX && latencies_i.LS_pipe.EX <= param.max_stalling));
    assume(0 < latencies_j.I_pipe.EX && latencies_j.I_pipe.EX <= param.max_stalling);
    assume(ops_j.LS_instr != load_store_op ==> (latencies_j.I_pipe.EX <= latencies_j.LS_pipe.EX && latencies_j.LS_pipe.EX <= param.max_stalling));
        
    // Assume variable latencies for load and store opcodes in EX stage
    // For previous instructions (i)
	// If EX stage not reached yet, then this will be either a cache hit or a cache miss
    assume((ops_i.LS_instr == load_store_op && stages_i.LS_pipe != EX) ==> ((latencies_i.I_pipe.EX <= latencies_i.LS_pipe.EX && latencies_i.LS_pipe.EX <= param.max_stalling) || latencies_i.LS_pipe.EX == param.max_latency));
    // If initially within EX stage, full range
    assume((ops_i.LS_instr == load_store_op && stages_i.LS_pipe == EX) ==> (0 < latencies_i.LS_pipe.EX && (latencies_i.LS_pipe.EX <= param.max_latency || latencies_i.LS_pipe.EX <= param.max_stalling)));
      
    // Current load/store instructions (j) will pass through EX, with either a cache hit or a cache miss
    assume(ops_j.LS_instr == load_store_op ==> ((latencies_j.I_pipe.EX <= latencies_j.LS_pipe.EX && latencies_j.LS_pipe.EX <= param.max_stalling) || latencies_j.LS_pipe.EX == param.max_latency));
    
    // Assume latencies for EX2 stage
    assume(0 < latencies_i.I_pipe.EX2 && latencies_i.I_pipe.EX2 <= param.max_stalling);
    assume(latencies_i.I_pipe.EX2 <= latencies_i.LS_pipe.EX2 && latencies_i.LS_pipe.EX2 <= param.max_stalling);
    assume(0 < latencies_j.I_pipe.EX2 && latencies_j.I_pipe.EX2 <= param.max_stalling);
    assume(latencies_j.I_pipe.EX2 <= latencies_j.LS_pipe.EX2 && latencies_j.LS_pipe.EX2 <= param.max_stalling);
      
    // Assume latencies for WB stage
    assume(0 < latencies_i.I_pipe.WB && latencies_i.I_pipe.WB <= param.max_stalling);
    assume(latencies_i.I_pipe.WB <= latencies_i.LS_pipe.WB && latencies_i.LS_pipe.WB <= param.max_stalling);
    assume(0 < latencies_j.I_pipe.WB && latencies_j.I_pipe.WB <= param.max_stalling);
    assume(latencies_j.I_pipe.WB <= latencies_j.LS_pipe.WB && latencies_j.LS_pipe.WB <= param.max_stalling);
    
    // Reduce state space by disabling variability: unused latencies since stages overtaken
    assume(stages_i.I_pipe == WB ==> (latencies_i.I_pipe.IF == 1 && latencies_i.I_pipe.ID == 1 && latencies_i.I_pipe.EX == 1 && latencies_i.I_pipe.EX2 == 1));
    assume(stages_i.I_pipe == EX2 ==> (latencies_i.I_pipe.IF == 1 && latencies_i.I_pipe.ID == 1 && latencies_i.I_pipe.EX == 1));
    assume(stages_i.I_pipe == EX ==> (latencies_i.I_pipe.IF == 1 && latencies_i.I_pipe.ID == 1));
    assume(stages_i.I_pipe == ID ==> (latencies_i.I_pipe.IF == 1));
    assume(stages_i.LS_pipe == WB ==> (latencies_i.LS_pipe.IF == 1 && latencies_i.LS_pipe.ID == 1 && latencies_i.LS_pipe.EX == 1 && latencies_i.LS_pipe.EX2 == 1));
    assume(stages_i.LS_pipe == EX2 ==> (latencies_i.LS_pipe.IF == 1 && latencies_i.LS_pipe.ID == 1 && latencies_i.LS_pipe.EX == 1));
    assume(stages_i.LS_pipe == EX ==> (latencies_i.LS_pipe.IF == 1 && latencies_i.LS_pipe.ID == 1));
    assume(stages_i.LS_pipe == ID ==> (latencies_i.LS_pipe.IF == 1));
    
    // Assume initial stages for previous instr.
    assume(stages_i.I_pipe == pre || stages_i.I_pipe == IF || stages_i.I_pipe == ID || stages_i.I_pipe == EX || stages_i.I_pipe == EX2 || stages_i.I_pipe == WB);
    assume(stages_i.LS_pipe == pre || stages_i.LS_pipe == IF || stages_i.LS_pipe == ID || stages_i.LS_pipe == EX || stages_i.LS_pipe == WB);
    // This is to allow only MAC operation to start from EX2 stage
    assume(stages_i.I_pipe == EX2 ==> ops_i.I_instr == mac_op);
    
    // Instruction pairs can split only after IF stage
    assume(stages_i.I_pipe == pre <==> stages_i.LS_pipe == pre);
    assume(stages_i.I_pipe == IF <==> stages_i.LS_pipe == IF);
  }

  next {
	next(cond);
  }
}
